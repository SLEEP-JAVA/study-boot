

buildscript {

    ext {
        springBootVersion = '2.1.6.RELEASE'
    }
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
        classpath "io.spring.gradle:dependency-management-plugin:1.0.6.RELEASE"
    }
}

allprojects {
    group 'com.sleep'
    version = '0.0.1-SNAPSHOT'
}

def javaProjects = subprojects - project('frontend')
configure(javaProjects) {
    apply plugin: 'java'
    apply plugin: 'org.springframework.boot'
    apply plugin: 'io.spring.dependency-management'

    sourceCompatibility = 11

    repositories {
        mavenCentral()
    }

    sourceSets {
        integrationTest {
            compileClasspath += main.output + test.output
            runtimeClasspath += main.output + test.output

            java {
                srcDirs = ['src/integration-test/java']
            }

            resources {
                srcDirs = ['src/integration-test/resources']
            }
        }
    }
    
    dependencies {
        compileOnly 'org.projectlombok:lombok:1.18.8'
        implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
        implementation 'org.springframework.boot:spring-boot-starter-web'
        runtimeOnly 'com.h2database:h2'
        runtimeOnly 'mysql:mysql-connector-java'
        annotationProcessor 'org.projectlombok:lombok'
        

        integrationTestAnnotationProcessor "org.projectlombok:lombok:1.18.8"
        testImplementation('org.springframework.security:spring-security-test')
        
        testCompileOnly("org.projectlombok:lombok:1.18.8")
        testImplementation("org.junit.jupiter:junit-jupiter-api:5.4.2")
        testImplementation("org.junit.jupiter:junit-jupiter-params:5.4.2")
        testRuntimeOnly("org.junit.jupiter:junit-jupiter-engine:5.4.2")
        testRuntimeOnly("org.junit.platform:junit-platform-launcher:1.4.2")
        testRuntimeOnly("org.junit.platform:junit-platform-commons:1.4.2")

        testImplementation("org.mockito:mockito-core:2.27.0")
        testImplementation("org.mockito:mockito-junit-jupiter:2.27.0")
        testImplementation('org.assertj:assertj-core:3.12.2')

        testImplementation('org.springframework.boot:spring-boot-starter-test') {
            exclude group: 'junit', module: 'junit'
            exclude group: 'org.assertj', module: 'assertj-core'
            exclude group: 'org.mockito', module: 'mockito-core'
        }
    }
}
