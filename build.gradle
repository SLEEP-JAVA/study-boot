

buildscript {

    ext {
        springBootVersion = '2.1.6.RELEASE'
        querydslPluginVersion = '1.0.10'
    }
    repositories {
        mavenCentral()
        maven { url 'https://plugins.gradle.org/m2/' }
    }
    dependencies {
        classpath "org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}"
        classpath "io.spring.gradle:dependency-management-plugin:1.0.6.RELEASE"
        classpath "gradle.plugin.com.ewerk.gradle.plugins:querydsl-plugin:${querydslPluginVersion}"

    }
}

allprojects {
    group 'com.sleep'
    version = '0.0.1-SNAPSHOT'
}

def javaProjects = subprojects - project('frontend')
configure(javaProjects) {
    apply plugin: 'java'
    apply plugin: 'org.springframework.boot'
    apply plugin: 'io.spring.dependency-management'
    apply plugin: "com.ewerk.gradle.plugins.querydsl"

    sourceCompatibility = 11

    repositories {
        mavenCentral()
    }

    sourceSets {
        integrationTest {
            compileClasspath += main.output + test.output
            runtimeClasspath += main.output + test.output

            java {
                srcDirs = ['src/integration-test/java']
            }

            resources {
                srcDirs = ['src/integration-test/resources']
            }
        }
    }
    
    dependencies {
        // querydsl
        compileOnly 'com.querydsl:querydsl-apt'
        compileOnly 'com.querydsl:querydsl-jpa'
        
        compileOnly 'org.projectlombok:lombok:1.18.8'
        implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
        implementation 'org.springframework.boot:spring-boot-starter-web'
        runtimeOnly 'com.h2database:h2'
        runtimeOnly 'mysql:mysql-connector-java'
        annotationProcessor 'org.projectlombok:lombok'

        testImplementation 'org.mockito:mockito-core:2.27.0'
        testImplementation 'org.mockito:mockito-junit-jupiter:2.27.0'
        testImplementation 'org.assertj:assertj-core:3.12.2'

        testImplementation('org.springframework.boot:spring-boot-starter-test') {
            exclude group: 'org.assertj', module: 'assertj-core'
            exclude group: 'org.mockito', module: 'mockito-core'
        }
    }

    // querydsl
    def querydslSrcDir = 'src/main/generated'

    querydsl {
        library = 'com.querydsl:querydsl-apt'
        jpa = true
        querydslSourcesDir = querydslSrcDir
    }

    sourceSets {
        main {
            java {
                srcDirs = ['src/main/java', querydslSrcDir]
            }
        }
    }
    
    compileQuerydsl{
        options.annotationProcessorPath = configurations.querydsl
    }

    configurations {
        querydsl.extendsFrom compileClasspath
    }

}

